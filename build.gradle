buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
    }
}

def SNAPSHOT = '1.0-SNAPSHOT'
version = SNAPSHOT
if (project.hasProperty("xodusVersion")) {
    version = project.xodusVersion
}

def jetbrainsRepositoryXodusPassword = 'pwd'
if (project.hasProperty("jetbrainsRepositoryXodusPassword")) {
    jetbrainsRepositoryXodusPassword = project.jetbrainsRepositoryXodusPassword;
}

subprojects {
    apply plugin: 'license'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.6
    compileJava.options.encoding = 'UTF-8'
    version = rootProject.version

    license {
        header rootProject.file('license/copyright.ftl')
        strictCheck true
        ext.inceptionYear = 2010
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.owner = 'JetBrains s.r.o.'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    jar {
        manifest {
            attributes "Implementation-Title": "Gradle", "Implementation-Version": version
        }
    }

    test {
        minHeapSize = '1g'
        maxHeapSize = '1g'
        jvmArgs = ['-ea', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=2808']
        testLogging.showStandardStreams = true;
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from project.sourceSets.main.java
    }

    def outputProjectName = rootProject.name + '-' + project.name;
    if (version == SNAPSHOT) {
        jar.archiveName = outputProjectName + '.jar'
        sourceJar.archiveName = outputProjectName + '-sources.jar'
    } else {
        jar.archiveName = outputProjectName + '-' + version + '.jar'
        sourceJar.archiveName = outputProjectName + '-' + version + '-sources.jar'
    }

    artifacts {
        archives sourceJar
    }

    if (project.name != 'benchmarks' && project.name != 'benchmarks-jmh' && project.name != 'samples') {
        publishing {
            publications {
                publishProject(MavenPublication) {
                    groupId 'org.jetbrains.xodus'
                    artifactId outputProjectName
                    if (jar.enabled) {
                        artifact jar
                    }
                    artifact sourceJar
                }
            }
            repositories {
                maven {
                    url "http://repository.jetbrains.com/xodus"
                    credentials {
                        username "xodus"
                        password jetbrainsRepositoryXodusPassword
                    }
                }
            }
        }
    }
}