buildscript {
    repositories { jcenter() }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version "1.2.4"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    compile project(':environment')
    compile 'io.github.microutils:kotlin-logging:1.4.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

license {
    include "**/*.kt"
    mapping {
        kt = 'JAVADOC_STYLE'
    }
}

//Shadow jar is a self-contained archive with all dependencies packed
jar.finalizedBy shadowJar

shadowJar {
    mustRunAfter jar
    archiveName = jar.archiveName
    manifest {
        attributes 'Main-Class': 'jetbrains.exodus.env.ReflectKt'
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

javadocJar {
    dependsOn dokkaJavadoc
    from dokkaJavadoc.outputDirectory
}

sourceJar {
    from project.sourceSets.main.kotlin
}

artifacts {
    shadowJar
}